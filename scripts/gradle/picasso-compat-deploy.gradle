apply plugin: 'maven'
apply plugin: 'signing'
apply from: '../scripts/gradle/gradle-commons.gradle'

ext {
    SDK_GROUP_ID = 'com.sebchlan.picassocompat'
    SDK_URL = 'https://github.com/schlan/picassocompat.git'
    PROJECT_NAME = 'PicassoCompat'
    PROJECT_DESCRIPTION = 'Make Picasso work'
    SDK_VERSION_NAME = rootProject.ext.versionName
    SDK_ARTIFACT_ID = project.ext.artifactId
    SDK_PACKAGING = project.ext.packaging
    POM_DEVELOPER_ID = 'sebchlan'
    POM_DEVELOPER_NAME = 'Sebastian Chlan'

    repoUsername = System.getenv("sebSonatypeUsername") ?: rootProject.properties["sebSonatypeUsername"]
    repoPassword = System.getenv("sebSonatypePassword") ?: rootProject.properties["sebSonatypePassword"]
}

ext."signing.keyId" = rootProject.properties["sebKeyId"]
ext."signing.secretKeyRingFile" = rootProject.properties["sebKeyRingFile"]
ext."signing.password" = rootProject.properties["sebKeyPassword"]

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: repoUsername, password: repoPassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: repoUsername, password: repoPassword)
                }

                pom.project {
                    name PROJECT_NAME
                    description PROJECT_DESCRIPTION
                    groupId SDK_GROUP_ID
                    artifactId SDK_ARTIFACT_ID
                    packaging SDK_PACKAGING
                    version SDK_VERSION_NAME
                    url SDK_URL

                    scm {
                        url SDK_URL
                        connection 'scm:git:https://github.com/schlan/picassocompat.git'
                        developerConnection 'scm:git:git@github.com:schlan/picassocompat.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.source
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.source
    }

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}